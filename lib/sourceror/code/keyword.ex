# Copyright 2024 Zach Daniel. All rights reserved.
# This file is licensed to you under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License. You may obtain a copy
# of the License at http://www.apache.org/licenses/LICENSE-2.0

# Unless required by applicable law or agreed to in writing, software distributed under
# the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR REPRESENTATIONS
# OF ANY KIND, either express or implied. See the License for the specific language
# governing permissions and limitations under the License.

# Branched from https://github.com/ash-project/igniter
# Initial implementation Copyright (c) 2024 Zach Daniel, licenced under Apache 2.0

defmodule Sourceror.Code.Keyword do
  @moduledoc """
  Utilities for working with keyword.
  """
  require Sourceror.Code.Common
  alias Sourceror.Code.Common
  alias Sourceror.Zipper

  @doc """
  Returns true if the node is a nested keyword list containing a value at the given path.

  ## Examples

      iex> zipper = Sourceror.parse_string!("[foo: [bar: 1]]") |> Sourceror.Zipper.zip()
      iex> Sourceror.Code.Keyword.keyword_has_path?(zipper, [:foo, :bar])
      true
      iex> Sourceror.Code.Keyword.keyword_has_path?(zipper, [:foo, :baz])
      false

  See also `get_key/2`.
  """
  @spec keyword_has_path?(Zipper.t(), [atom()]) :: boolean()
  def keyword_has_path?(_zipper, []), do: true

  def keyword_has_path?(zipper, [key | rest]) do
    case get_key(zipper, key) do
      {:ok, zipper} -> keyword_has_path?(zipper, rest)
      :error -> false
    end
  end

  @doc """
  Moves the zipper to the value of `key` in a keyword list.

  ## Examples

      iex> zipper = Sourceror.parse_string!("[foo: 1, bar: 2]") |> Sourceror.Zipper.zip()
      iex> {:ok, result} = Sourceror.Code.Keyword.get_key(zipper, :bar)
      iex> match?({:__block__, _, [2]}, result.node)
      true

  See also `keyword_has_path?/2`.
  """
  @spec get_key(Zipper.t(), atom()) :: {:ok, Zipper.t()} | :error
  def get_key(zipper, key) do
    zipper = Common.maybe_move_to_single_child_block(zipper)

    if Sourceror.Code.List.list?(zipper) do
      item =
        Sourceror.Code.List.move_to_list_item(zipper, fn item ->
          if Sourceror.Code.Tuple.tuple?(item) do
            case Sourceror.Code.Tuple.tuple_elem(item, 0) do
              {:ok, first_elem} ->
                Common.nodes_equal?(first_elem, key)

              :error ->
                false
            end
          end
        end)

      case item do
        {:ok, zipper} -> Sourceror.Code.Tuple.tuple_elem(zipper, 1)
        :error -> :error
      end
    else
      :error
    end
  end

  @doc """
  Puts a value at a path into a keyword, calling `updater` on the zipper at the value if the key is already present.

  ## Examples

      iex> zipper = Sourceror.parse_string!("[foo: 1]") |> Sourceror.Zipper.zip()
      iex> {:ok, zipper} = Sourceror.Code.Keyword.put_in_keyword(zipper, [:bar], 2)
      iex> Sourceror.to_string(zipper.node) |> String.contains?("bar:")
      true

  See also `set_keyword_key/4`.
  """
  @spec put_in_keyword(
          Zipper.t(),
          list(atom()),
          term(),
          (Zipper.t() -> {:ok, Zipper.t()} | :error) | nil
        ) ::
          {:ok, Zipper.t()} | :error
  def put_in_keyword(zipper, path, value, updater \\ nil) do
    updater = updater || fn zipper -> {:ok, Common.replace_code(zipper, value)} end

    Common.within(zipper, fn zipper ->
      do_put_in_keyword(zipper, path, value, updater)
    end)
  end

  defp do_put_in_keyword(zipper, [key], value, updater) do
    set_keyword_key(zipper, key, value, updater)
  end

  defp do_put_in_keyword(zipper, [key | rest], value, updater) do
    case create_or_move_to_value_for_key(zipper, key) do
      {:found, zipper} ->
        do_put_in_keyword(zipper, rest, value, updater)

      {:new, zipper} ->
        {:ok, set_keyword_value!(zipper, keywordify(rest, value))}

      :error ->
        :error
    end
  end

  @doc """
  Puts a key into a keyword, calling `updater` on the zipper at the value if the key is already present.

  ## Examples

      iex> zipper = Sourceror.parse_string!("[foo: 1]") |> Sourceror.Zipper.zip()
      iex> {:ok, zipper} = Sourceror.Code.Keyword.set_keyword_key(zipper, :bar, 2)
      iex> Sourceror.to_string(zipper.node) |> String.contains?("bar:")
      true

  See also `put_in_keyword/4`.
  """
  @spec set_keyword_key(
          Zipper.t(),
          atom(),
          term(),
          (Zipper.t() -> {:ok, Zipper.t()} | :error) | nil
        ) ::
          {:ok, Zipper.t()} | :error
  def set_keyword_key(zipper, key, value, updater \\ nil) do
    updater = updater || (&{:ok, &1})

    Common.within(zipper, fn zipper ->
      with {:found, zipper} <- create_or_move_to_value_for_key(zipper, key),
           {:ok, zipper} <- updater.(zipper) do
        {:ok, %{zipper | node: {:__block__, [], [zipper.node]}}}
      else
        {:new, zipper} -> {:ok, set_keyword_value!(zipper, value)}
        :error -> :error
        {:ok, _} = other -> other
      end
    end)
  end

  defp set_keyword_value!(zipper, value) do
    value =
      value
      |> Sourceror.to_string()
      |> Sourceror.parse_string!()

    Zipper.replace(zipper, value)
  end

  @spec create_or_move_to_value_for_key(Zipper.t(), atom()) ::
          {:found, Zipper.t()} | {:new, Zipper.t()} | :error
  defp create_or_move_to_value_for_key(zipper, key) do
    zipper = Common.maybe_move_to_single_child_block(zipper)

    if Sourceror.Code.List.list?(zipper) do
      case get_key(zipper, key) do
        {:ok, zipper} ->
          {:found, zipper}

        :error ->
          to_append =
            case zipper.node do
              [{{:__block__, meta, _}, _} | _] ->
                if meta[:format] do
                  {{:__block__, [format: meta[:format]], [key]}, {:__block__, [], [nil]}}
                else
                  {{:__block__, [], [key]}, {:__block__, [], [nil]}}
                end

              [] ->
                {{:__block__, [format: :keyword], [key]}, {:__block__, [], [nil]}}

              _ ->
                {{:__block__, [], [key]}, {:__block__, [], [nil]}}
            end

          with {:ok, zipper} <- zipper |> Zipper.append_child(to_append) |> get_key(key) do
            {:new, zipper}
          end
      end
    else
      :error
    end
  end

  @doc """
  Removes a key from a keyword list if present. Returns `:error` only if the node is not a list.

  ## Examples

      iex> zipper = Sourceror.parse_string!("[foo: 1, bar: 2]") |> Sourceror.Zipper.zip()
      iex> {:ok, zipper} = Sourceror.Code.Keyword.remove_keyword_key(zipper, :foo)
      iex> Sourceror.to_string(zipper.node) |> String.contains?("foo:")
      false

  See also `set_keyword_key/4`.
  """
  @spec remove_keyword_key(Zipper.t(), atom()) :: {:ok, Zipper.t()} | :error
  def remove_keyword_key(zipper, key) do
    Sourceror.Code.List.remove_from_list(zipper, fn zipper ->
      Sourceror.Code.Tuple.elem_equals?(zipper, 0, key)
    end)
  end

  @doc """
  Puts into nested keyword lists represented by `path`.

  ## Examples

      iex> Sourceror.Code.Keyword.keywordify([:foo, :bar], 1)
      [{{:__block__, [format: :keyword], [:foo]}, {:__block__, [], [[{{:__block__, [format: :keyword], [:bar]}, {:__block__, [], [1]}}]]}}]
  """
  @spec keywordify(path :: [atom()], value :: any()) :: any()
  def keywordify([], value) when is_integer(value) or is_float(value) do
    {:__block__, [token: to_string(value)], [value]}
  end

  def keywordify([], value) do
    {:__block__, [], [value]}
  end

  def keywordify([key | rest], value) do
    [{{:__block__, [format: :keyword], [key]}, {:__block__, [], [keywordify(rest, value)]}}]
  end
end
